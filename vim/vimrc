" Default vim path ini all versions
set rtp +=~/.vim

" vim plug
if empty(glob('~/.vim/autoload/plug.vim'))
  silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
        \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
    autocmd VimEnter * PlugInstall
endif

" Helper conditional install bundle
function! Cond(cond, ...)
  let opts = get(a:000, 0, {})
  return a:cond ? opts : extend(opts, { 'on': [], 'for': [] })
endfunction

" bundles
call plug#begin('~/.vim/plugged')
    for fpath in split(globpath('~/dotfiles/vim/bundles', '*.vim'), '\n')
        exe 'source' fpath
    endfor

    if filereadable(expand('~/.vim/bundles.vim'))
        source ~/.vim/bundles.vim
    endif
call plug#end()

" settings
for fpath in split(globpath('~/dotfiles/vim/settings', '*.vim'), '\n')
  exe 'source' fpath
endfor

if filereadable(expand('~/.vim/settings.vim'))
    source ~/.vim/settings.vim
endif

" map <silent> <leader>li :set list!<bar>set nolist?<cr>
map <silent> \hl :set hls!<bar>set nohls?<cr>
map <silent> \nu :set rnu!<bar>set rnu?<cr>
map <silent> \wr :set wrap!<bar>set wrap?<cr>

" Spell
set spellcapcheck=[.?!]\_[\])'"^I ]\+
map <silent> \pt :set spell spelllang=pt<cr>
map <silent> \en :set spell spelllang=en<cr>
map <silent> \sp :set spell!<cr>

noremap <silent> \ve :let &virtualedit = (&virtualedit == "all" ? "" : "all")<bar>set virtualedit?<CR>

" trim
fun! <SID>StripTrailingWhitespaces()
    let l = line(".")
    let c = col(".")
    %s/\s\+$//e
    call cursor(l, c)
endfun
map <silent>\tr :call <SID>StripTrailingWhitespaces()<CR>

" Shortcuts for nav buffer
nmap <silent> <leader><Tab> :b#<cr>

" vim:noet:foldmethod=marker
